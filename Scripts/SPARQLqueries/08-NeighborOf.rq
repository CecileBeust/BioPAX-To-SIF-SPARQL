PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX chebi: <http://purl.obolibrary.org/obo/chebi/>
PREFIX chebidb: <http://purl.obolibrary.org/obo/CHEBI_>
PREFIX chebirel: <http://purl.obolibrary.org/obo/CHEBI#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX bp3: <http://www.biopax.org/release/biopax-level3.owl#>
PREFIX reactome: <http://www.reactome.org/biopax/93/48887#>
PREFIX abstraction:<http://abstraction/#>

CONSTRUCT {
   ?prot1Ref abstraction:NeighborOf ?prot2Ref
}
WHERE {
  ?reaction rdf:type/(rdfs:subClassOf*) bp3:Interaction .
  
  ?reaction ((^bp3:controlled/bp3:controller)|bp3:left|bp3:right) ?prot1 .
  ?prot1 rdf:type bp3:Protein .
  ?prot1 bp3:entityReference ?prot1Ref .
  ?prot1Ref rdf:type bp3:ProteinReference .
  ?reaction ((^bp3:controlled/bp3:controller)|bp3:left|bp3:right) ?prot2 .
  ?prot2 rdf:type bp3:Protein .
  ?prot2 bp3:entityReference ?prot2Ref .
  ?prot2Ref rdf:type bp3:ProteinReference .
  FILTER (STR(?prot1Ref) < STR(?prot2Ref))
}